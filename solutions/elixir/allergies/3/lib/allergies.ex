import Bitwise, only: [&&&: 2]

defmodule Allergies do
  @doc """
  List the allergies for which the corresponding flag bit is true.
  """
  @spec list(non_neg_integer) :: [String.t()]
  def list(flags) do
    allergens()
    |> Enum.filter(fn {_name, value} -> (flags &&& value) != 0 end)
    |> Enum.map(fn {name, _} -> name end)
  end

  @doc """
  Returns whether the corresponding flag bit in 'flags' is set for the item.
  """
  @spec allergic_to?(non_neg_integer, String.t()) :: boolean
  def allergic_to?(flags, item) do
    case Map.get(values_map(), item) do
      nil -> false
      value -> (flags &&& value) != 0
    end
  end


  defp allergens do
    [
      {"eggs", 1},
      {"peanuts", 2},
      {"shellfish", 4},
      {"strawberries", 8},
      {"tomatoes", 16},
      {"chocolate", 32},
      {"pollen", 64},
      {"cats", 128}
    ]
  end

  defp values_map do
    Map.new(allergens())
  end
end
